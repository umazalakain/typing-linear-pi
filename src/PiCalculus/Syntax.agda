{-# OPTIONS --safe --without-K #-}

open import Data.String.Base using (String)

module PiCalculus.Syntax where

Name : Set
Name = String

module Raw where
  infix 20 _âˆ¥_
  infixr 15 â¦…Ï…_â¦†_
  infixr 9 _â¦…_â¦†_
  infixr 9 _âŸ¨_âŸ©_

  data Raw : Set where
    ğŸ˜     : Raw
    â¦…Ï…_â¦†_ : Name â†’ Raw â†’ Raw
    _âˆ¥_   : Raw â†’ Raw â†’ Raw
    _â¦…_â¦†_ : Name â†’ Name â†’ Raw â†’ Raw
    _âŸ¨_âŸ©_ : Name â†’ Name â†’ Raw â†’ Raw


module Scoped where
  open import Data.Fin.Base
  open import Data.Nat.Base

  infix 20 _âˆ¥_
  infixr 15 Ï…
  infixr 9 _â¦…â¦†_
  infixr 9 _âŸ¨_âŸ©_

  private
    variable
      n : â„•

  data Scoped : â„• â†’ Set where
    ğŸ˜     : Scoped n
    Ï… : Scoped (suc n) â†’ â¦ƒ name : Name â¦„ â†’ Scoped n
    _âˆ¥_   : Scoped n â†’ Scoped n â†’ Scoped n
    _â¦…â¦†_ : Fin n â†’ Scoped (suc n) â†’ â¦ƒ name : Name â¦„ â†’ Scoped n
    _âŸ¨_âŸ©_ : Fin n â†’ Fin n â†’ Scoped n â†’ Scoped n

module Conversion where
  private
    open Raw
    open Scoped

    open import Level using (Lift; _âŠ”_)
    open import Relation.Binary.PropositionalEquality using (_â‰¡_; _â‰¢_; refl; sym)
    open import Function using (_âˆ˜_)
    open import Relation.Nullary using (Dec; yes; no; _because_)
    open import Relation.Nullary.Decidable using (True; toWitness)
    open import Relation.Nullary.Product using (_Ã—-dec_)
    open import Relation.Nullary.Negation using (Â¬?)

    import Data.Vec.Base as Vec
    import Data.Char.Base as Char
    import Data.List.Base as List
    import Data.String.Base as String
    import Data.String.Properties as Stringâ‚š
    import Data.Vec.Membership.DecPropositional as DecPropositional

    open import Data.Empty using (âŠ¥)
    open import Data.Bool.Base using (true; false)
    open import Data.Product using (_,_; _Ã—_; Î£)
    open import Data.Unit using (âŠ¤; tt)
    open import Data.Nat.Base using (â„•; zero; suc)
    open import Data.Fin.Base using (Fin; zero; suc)
    open import Data.String.Base using (_++_)
    open import Data.Vec.Membership.Propositional using (_âˆˆ_; _âˆ‰_)
    open import Data.Vec.Relation.Unary.Any using (here; there)

    open Vec using (Vec; []; _âˆ·_)
    open List using (List; []; _âˆ·_)

    _âˆˆ?_ = DecPropositional._âˆˆ?_ Stringâ‚š._â‰Ÿ_

    import PiCalculus.Utils
    module AllAcc = PiCalculus.Utils.AllAcc
    module â„•â‚› = PiCalculus.Utils.â„•â‚›
    open AllAcc using ([]; _âˆ·_)

    variable
      n m : â„•

  Ctx : â„• â†’ Set
  Ctx = Vec Name

  count : Name â†’ Ctx n â†’ â„•
  count hint [] = zero
  count hint (name âˆ· ctx) with name Stringâ‚š.â‰Ÿ hint
  count hint (name âˆ· ctx) | true because _ = suc (count hint ctx)
  count hint (name âˆ· ctx) | false because _ = count hint ctx

  CountedName : Name â†’ Ctx n â†’ Set
  CountedName name ctx = Î£ â„• (count name ctx â‰¡_)

  Fresh : Ctx n â†’ Set
  Fresh = AllAcc.All CountedName

  -- From contexts to name counts
  fresh : âˆ€ name (ctx : Ctx n) â†’ CountedName name ctx
  fresh hint ctx = count hint ctx , refl

  -- From name counts to tuples
  erase : âˆ€ {x} (xs : Vec Name n) â†’ CountedName x xs â†’ Name Ã— â„•
  erase {x = x} xs (i , _) = x , i

  -- From tuples to strings, convert to lists first so that we can reason about it
  toCharList : Name Ã— â„• â†’ List Char.Char
  toCharList (x , i) = String.toList x List.++ ('^' âˆ· â„•â‚›.toDigitChars 10 i)

  toString : Name Ã— â„• â†’ Name
  toString = String.fromList âˆ˜ toCharList

  repr : âˆ€ {x} (xs : Vec Name n) â†’ CountedName x xs â†’ Name
  repr xs = toString âˆ˜ erase xs

  apply : {ctx : Ctx n} â†’ Fresh ctx â†’ Ctx n
  apply = Vec.map toString âˆ˜ AllAcc.map Î» { {xs = xs} â†’ erase xs}

  WellScoped : Ctx n â†’ Raw â†’ Set
  WellScoped ctx ğŸ˜ = âŠ¤
  WellScoped ctx (â¦…Ï… x â¦† P) = WellScoped (x âˆ· ctx) P
  WellScoped ctx (P âˆ¥ Q) = WellScoped ctx P Ã— WellScoped ctx Q
  WellScoped ctx (x â¦… y â¦† P) = (x âˆˆ ctx) Ã— WellScoped (y âˆ· ctx) P
  WellScoped ctx (x âŸ¨ y âŸ© P) = (x âˆˆ ctx) Ã— (y âˆˆ ctx) Ã— WellScoped ctx P

  WellScoped? : (ctx : Ctx n) (P : Raw) â†’ Dec (WellScoped ctx P)
  WellScoped? ctx ğŸ˜ = yes tt
  WellScoped? ctx (â¦…Ï… x â¦† P) = WellScoped? (x âˆ· ctx) P
  WellScoped? ctx (P âˆ¥ Q) = WellScoped? ctx P Ã—-dec WellScoped? ctx Q
  WellScoped? ctx (x â¦… y â¦† P) = x âˆˆ? ctx Ã—-dec WellScoped? (y âˆ· ctx) P
  WellScoped? ctx (x âŸ¨ y âŸ© P) = x âˆˆ? ctx Ã—-dec y âˆˆ? ctx Ã—-dec WellScoped? ctx P

  NotShadowed : Ctx n â†’ Raw â†’ Set
  NotShadowed ctx ğŸ˜ = âŠ¤
  NotShadowed ctx (â¦…Ï… name â¦† P) = name âˆ‰ ctx Ã— NotShadowed (name âˆ· ctx) P
  NotShadowed ctx (P âˆ¥ Q) = NotShadowed ctx P Ã— NotShadowed ctx Q
  NotShadowed ctx (x â¦… y â¦† P) = y âˆ‰ ctx Ã— NotShadowed (y âˆ· ctx) P
  NotShadowed ctx (x âŸ¨ y âŸ© P) = NotShadowed ctx P

  NotShadowed? : (ctx : Ctx n) (P : Raw) â†’ Dec (NotShadowed ctx P)
  NotShadowed? ctx ğŸ˜ = yes tt
  NotShadowed? ctx (â¦…Ï… name â¦† P) = Â¬? (name âˆˆ? ctx) Ã—-dec NotShadowed? (name âˆ· ctx) P
  NotShadowed? ctx (P âˆ¥ Q) = NotShadowed? ctx P Ã—-dec NotShadowed? ctx Q
  NotShadowed? ctx (x â¦… y â¦† P) = Â¬? (y âˆˆ? ctx) Ã—-dec NotShadowed? (y âˆ· ctx) P
  NotShadowed? ctx (x âŸ¨ y âŸ© P) = NotShadowed? ctx P

  âˆˆtoFin : âˆ€ {a} {A : Set a} {x} {xs : Vec A n} â†’ x âˆˆ xs â†’ Fin n
  âˆˆtoFin (here px) = zero
  âˆˆtoFin (there xâˆˆxs) = suc (âˆˆtoFin xâˆˆxs)

  fromRaw' : (ctx : Ctx n) (P : Raw) â†’ WellScoped ctx P â†’ Scoped n
  fromRaw' ctx ğŸ˜ tt = ğŸ˜
  fromRaw' ctx (â¦…Ï… x â¦† P) wsP =
    Ï… (fromRaw' (x âˆ· ctx) P wsP) â¦ƒ x â¦„
  fromRaw' ctx (P âˆ¥ Q) (wsP , wsQ) =
    fromRaw' ctx P wsP âˆ¥ fromRaw' ctx Q wsQ
  fromRaw' ctx (x â¦… y â¦† P) (xâˆˆctx , wsP) =
    (âˆˆtoFin xâˆˆctx â¦…â¦† fromRaw' (y âˆ· ctx) P wsP) â¦ƒ y â¦„
  fromRaw' ctx (x âŸ¨ y âŸ© P) (xâˆˆctx , yâˆˆctx , wsP) =
    âˆˆtoFin xâˆˆctx âŸ¨ âˆˆtoFin yâˆˆctx âŸ© fromRaw' ctx P wsP

  fromRaw : (ctx : Ctx n) (P : Raw) â†’ â¦ƒ _ : True (WellScoped? ctx P) â¦„ â†’ Scoped n
  fromRaw ctx P â¦ƒ p â¦„ = fromRaw' ctx P (toWitness p)

  toRaw : {ctx : Ctx n} â†’ Fresh ctx â†’ Scoped n â†’ Raw
  toRaw {ctx = ctx} isf ğŸ˜ = ğŸ˜
  toRaw {ctx = ctx} isf (Ï… P â¦ƒ name â¦„) =
    let cname = fresh name ctx in
    â¦…Ï… repr ctx cname â¦† toRaw (cname âˆ· isf) P
  toRaw {ctx = ctx} isf (P âˆ¥ Q) =
    toRaw isf P âˆ¥ toRaw isf Q
  toRaw {ctx = ctx} isf ((x â¦…â¦† P) â¦ƒ name â¦„) =
    let cname = fresh name ctx in
    Vec.lookup (apply isf) x â¦… repr ctx cname â¦† toRaw (cname âˆ· isf) P
  toRaw {ctx = ctx} isf (x âŸ¨ y âŸ© P) =
    Vec.lookup (apply isf) x âŸ¨ Vec.lookup (apply isf) y âŸ© toRaw isf P

  map : âˆ€ {a} (B : Scoped n â†’ Set a) (ctx : Vec Name n) (P : Raw) â†’ Set a
  map B ctx P with WellScoped? ctx P
  map B ctx P | yes wsP = B (fromRaw' ctx P wsP)
  map B ctx P | no _ = Lift _ âŠ¥

  mapâ‚‚ : âˆ€ {a} (B : Scoped n â†’ Scoped n â†’ Set a) (ctx : Vec Name n) (P Q : Raw) â†’ Set a
  mapâ‚‚ B ctx P Q with WellScoped? ctx P | WellScoped? ctx Q
  mapâ‚‚ B ctx P Q | yes wsP | yes wsQ = B (fromRaw' ctx P wsP) (fromRaw' ctx Q wsQ)
  mapâ‚‚ B ctx P Q | _       | _       = Lift _ âŠ¥
