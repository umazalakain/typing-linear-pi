{-# OPTIONS --safe --without-K #-}

open import Relation.Binary.PropositionalEquality using (_â‰¡_; _â‰¢_; refl; sym; cong; congâ‚‚; subst; trans)
open Relation.Binary.PropositionalEquality.â‰¡-Reasoning
open import Function using (_âˆ˜_)
open import Relation.Nullary using (yes; no)

open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Data.Product using (_Ã—_; _,_; Î£-syntax; projâ‚; projâ‚‚)
open import Data.Fin.Base using (Fin; zero; suc)
open import Data.Unit using (âŠ¤; tt)

import Data.List.Base as List
import Data.List.Properties as Listâ‚š
import Data.List.Membership.Propositional as âˆˆâ‚—
import Data.Nat.Base as â„•
import Data.Nat.Properties as â„•â‚š
import Data.String.Base as String
import Data.Product.Properties as Productâ‚š
import Data.Vec.Base as Vec
import Data.Vec.Relation.Unary.Any as Any
import Data.Vec.Membership.Propositional as âˆˆáµ¥
import Data.Vec.Membership.Propositional.Properties as âˆˆáµ¥â‚š
import Data.String.Properties as Stringâ‚š

open Vec using ([]; _âˆ·_; Vec)
open String using (String)
open â„• using (â„•; zero; suc)
open Any using (here; there)
open List using (List; []; _âˆ·_; [_])

open import PiCalculus.Syntax
open Raw
open Scoped
open Conversion

open import PiCalculus.Utils
open AllAcc using ([]; _âˆ·_)

module PiCalculus.Syntax.Properties where

module _ where
  private
    variable
      n : â„•
      P Q R S : Scoped n
      x y : Fin n
      namex namey : Name

  fromNameâˆ˜toName : (i : Fin n) (ctx : Ctx n) â†’ âˆˆtoFin (âˆˆáµ¥â‚š.âˆˆ-lookup i ctx) â‰¡ i
  fromNameâˆ˜toName zero (x âˆ· ctx) = refl
  fromNameâˆ˜toName (suc i) (x âˆ· ctx) rewrite fromNameâˆ˜toName i ctx = refl

  toNameâˆ˜fromName : âˆ€ {x} {ctx : Ctx n} (xâˆˆctx : x âˆˆáµ¥.âˆˆ ctx) â†’ Vec.lookup ctx (âˆˆtoFin xâˆˆctx) â‰¡ x
  toNameâˆ˜fromName (here px) = sym px
  toNameâˆ˜fromName (there xâˆˆctx) = toNameâˆ˜fromName xâˆˆctx

  postulate
    -- PR agda accepted, landing in 2.6.2 https://github.com/agda/agda/pull/4790
    fromList-injective : âˆ€ a b â†’ String.fromList a â‰¡ String.fromList b â†’ a â‰¡ b

  -- The circum (^) is not a decimal character
  ^âˆ‰DECIMALS : '^' âˆˆáµ¥.âˆ‰ â„•â‚›.DECIMALS
  ^âˆ‰DECIMALS (there (there (there (there (there (there (there (there (there (there ()))))))))))

  -- In <name>^<natural> the <natural> does not contain ^, therefore toString is injective
  toString-injective : (x y : Name Ã— â„•) â†’ toString x â‰¡ toString y â†’ x â‰¡ y
  toString-injective (nx , cx) (ny , cy) eq = congâ‚‚ _,_ strip-toList strip-toDecimalChars
    where
      strip-fromList = fromList-injective (toCharList (nx , cx)) (toCharList (ny , cy)) eq
      count-repr = ListInv.inv-++Ê³ (String.toList nx) (String.toList ny) '^'
                                  (^âˆ‰DECIMALS âˆ˜ (â„•â‚›.âˆˆtoDigitCharsâ‡’âˆˆdigitChars cx '^'))
                                  (^âˆ‰DECIMALS âˆ˜ (â„•â‚›.âˆˆtoDigitCharsâ‡’âˆˆdigitChars cy '^'))
                                  strip-fromList
      strip-toDecimalChars = â„•â‚›.toDigitChars-injective cx cy count-repr
      cancel-names = Listâ‚š.++-cancelÊ³ (String.toList nx) (String.toList ny)
                                      (subst (Î» â— â†’ String.toList nx List.++ ('^' âˆ· â—) â‰¡ _)
                                            count-repr strip-fromList)
      strip-toList = Stringâ‚š.toList-injective nx ny cancel-names


  -- A fresh variable name created from inspecting a context cannot be in that context
  fresh-âˆ‰' : âˆ€ m name (xs : Ctx n) â†’ toString (name , m â„•.+ (count name xs)) âˆˆáµ¥.âˆ‰ apply xs
  fresh-âˆ‰' m name (x âˆ· xs) (here seq) with x Stringâ‚š.â‰Ÿ name
  ... | yes refl = â„•â‚š.mâ‰¢1+n+m _ (begin
    count name xs
      â‰¡Ë˜âŸ¨ Productâ‚š.,-injectiveÊ³ (toString-injective (name , m â„•.+ suc (count name xs)) (name , count name xs) seq) âŸ©
    m â„•.+ suc (count name xs)
      â‰¡âŸ¨ â„•â‚š.+-suc m _ âŸ©
    suc m â„•.+ count name xs
      âˆ)
  ... | no Â¬q = Â¬q (Productâ‚š.,-injectiveË¡ (sym (toString-injective (name , m â„•.+ count name xs) (x , count x xs) seq)))
  fresh-âˆ‰' m name (x âˆ· xs) (there âˆˆps) with x Stringâ‚š.â‰Ÿ name
  fresh-âˆ‰' m name (x âˆ· xs) (there âˆˆps) | yes refl rewrite â„•â‚š.+-suc m (count name xs) = fresh-âˆ‰' (suc m) name _ âˆˆps
  fresh-âˆ‰' m name (x âˆ· xs) (there âˆˆps) | no Â¬q = fresh-âˆ‰' m name _ âˆˆps

  fresh-âˆ‰ : âˆ€ name (xs : Ctx n) â†’ toString (name , count name xs) âˆˆáµ¥.âˆ‰ apply xs
  fresh-âˆ‰ name xs = fresh-âˆ‰' zero name xs

  -- Translating from de Bruijn to names results in a well-scoped process

  toRaw-WellScoped : (ctx : Ctx n) (P : Scoped n) â†’ WellScoped (apply ctx) (toRaw ctx P)
  toRaw-WellScoped ctx ğŸ˜ = tt
  toRaw-WellScoped ctx (Î½ P â¦ƒ name â¦„) = toRaw-WellScoped (name âˆ· ctx) P
  toRaw-WellScoped ctx (P âˆ¥ Q) = toRaw-WellScoped ctx P , toRaw-WellScoped ctx Q
  toRaw-WellScoped ctx ((x â¦…â¦† P) â¦ƒ name â¦„) = âˆˆáµ¥â‚š.âˆˆ-lookup _ _ , toRaw-WellScoped (name âˆ· ctx) P
  toRaw-WellScoped ctx (x âŸ¨ y âŸ© P) = âˆˆáµ¥â‚š.âˆˆ-lookup _ _ , âˆˆáµ¥â‚š.âˆˆ-lookup _ _ , toRaw-WellScoped ctx P

  -- Translating from de Bruijn to names results in no shadowed variables

  toRaw-NotShadowed : (ctx : Ctx n) (P : Scoped n) â†’ NotShadowed (apply ctx) (toRaw ctx P)
  toRaw-NotShadowed ctx ğŸ˜ = tt
  toRaw-NotShadowed ctx (Î½ P â¦ƒ name â¦„) = fresh-âˆ‰ name ctx , (toRaw-NotShadowed (_ âˆ· ctx) P)
  toRaw-NotShadowed ctx (P âˆ¥ Q) = toRaw-NotShadowed ctx P , toRaw-NotShadowed ctx Q
  toRaw-NotShadowed ctx ((x â¦…â¦† P) â¦ƒ name â¦„) = fresh-âˆ‰ name ctx , toRaw-NotShadowed (name âˆ· ctx) P
  toRaw-NotShadowed ctx (x âŸ¨ y âŸ© P) = toRaw-NotShadowed ctx P

  -- Translating from de Bruijn to names and back results in the same process modulo name hints

  data _Î±-â‰¡_ {n} : Scoped n â†’ Scoped n â†’ Set where
    inaction : ğŸ˜ Î±-â‰¡ ğŸ˜
    scope    : P Î±-â‰¡ Q â†’ Î½ P â¦ƒ namex â¦„ Î±-â‰¡ Î½ Q â¦ƒ namey â¦„
    comp     : P Î±-â‰¡ Q â†’ R Î±-â‰¡ S â†’ (P âˆ¥ R) Î±-â‰¡ (Q âˆ¥ S)
    input    : P Î±-â‰¡ Q â†’ (x â¦…â¦† P) â¦ƒ namex â¦„ Î±-â‰¡ (x â¦…â¦† Q) â¦ƒ namey â¦„
    output   : P Î±-â‰¡ Q â†’ (x âŸ¨ y âŸ© P) Î±-â‰¡ (x âŸ¨ y âŸ© Q)

  fromRawâˆ˜toRaw : (ctx : Ctx n) (P : Scoped n)
                â†’ fromRaw' (apply ctx) (toRaw ctx P) (toRaw-WellScoped ctx P) Î±-â‰¡ P
  fromRawâˆ˜toRaw ctx ğŸ˜ = inaction
  fromRawâˆ˜toRaw ctx (Î½ P â¦ƒ name â¦„) =
    scope (fromRawâˆ˜toRaw (name âˆ· ctx) P)
  fromRawâˆ˜toRaw ctx (P âˆ¥ Q) =
    comp (fromRawâˆ˜toRaw ctx P) (fromRawâˆ˜toRaw ctx Q)
  fromRawâˆ˜toRaw ctx ((x â¦…â¦† P) â¦ƒ name â¦„)
    rewrite fromNameâˆ˜toName x (apply ctx) =
    input (fromRawâˆ˜toRaw (name âˆ· ctx) P)
  fromRawâˆ˜toRaw ctx (x âŸ¨ y âŸ© P)
    rewrite fromNameâˆ˜toName x (apply ctx) | fromNameâˆ˜toName y (apply ctx) =
    output (fromRawâˆ˜toRaw ctx P)


module _ where
  private
    variable
      n : â„•
      P Q R S : Raw
      x y w z : Name
      ks vs : Ctx n

  _âˆˆÂ²_ : âˆ€ {n} â†’ (Name Ã— Name) â†’ (Ctx n Ã— Ctx n) â†’ Set
  (x , y ) âˆˆÂ² (xs , ys) = Î£[ i âˆˆ Fin _ ] (Vec.lookup xs i â‰¡ x Ã— Vec.lookup ys i â‰¡ y)

  infix 5 _Î±[_â†¦_]â‰¡_
  data _Î±[_â†¦_]â‰¡_ : Raw â†’ âˆ€ {n} â†’ Ctx n â†’ Ctx n â†’ Raw â†’ Set where
    inaction : ğŸ˜ Î±[ ks â†¦ vs ]â‰¡ ğŸ˜
    scope    : P Î±[ x âˆ· ks â†¦ y âˆ· vs ]â‰¡ Q
             â†’ â¦…Î½ x â¦† P Î±[ ks â†¦ vs ]â‰¡ â¦…Î½ y â¦† Q
    comp     : P Î±[ ks â†¦ vs ]â‰¡ Q
             â†’ R Î±[ ks â†¦ vs ]â‰¡ S
             â†’ P âˆ¥ R Î±[ ks â†¦ vs ]â‰¡ Q âˆ¥ S
    input    : (x , y) âˆˆÂ² (ks , vs)
             â†’ P Î±[ w âˆ· ks â†¦ z âˆ· vs ]â‰¡ Q
             â†’ x â¦… w â¦† P Î±[ ks â†¦ vs ]â‰¡ y â¦… z â¦† Q
    output   : (x , y) âˆˆÂ² (ks , vs)
             â†’ (w , z) âˆˆÂ² (ks , vs)
             â†’ P Î±[ ks â†¦ vs ]â‰¡ Q
             â†’ x âŸ¨ w âŸ© P Î±[ ks â†¦ vs ]â‰¡ (y âŸ¨ z âŸ© Q)

  -- Translating a well-scoped process to de Bruijn and back results in the same process
  -- modulo alpha renaming, where the new names in `apply isf` map to the old in `ctx`

  toRawâˆ˜fromRaw : (ctx : Ctx n) (P : Raw) (wsP : WellScoped ctx P)
                â†’ toRaw ctx (fromRaw' ctx P wsP) Î±[ apply ctx â†¦ ctx ]â‰¡ P
  toRawâˆ˜fromRaw ctx ğŸ˜ wsP = inaction
  toRawâˆ˜fromRaw ctx (â¦…Î½ x â¦† P) wsP
    = scope (toRawâˆ˜fromRaw (x âˆ· ctx) P wsP)
  toRawâˆ˜fromRaw ctx (P âˆ¥ Q) (wsP , wsQ)
    = comp (toRawâˆ˜fromRaw ctx P wsP)
           (toRawâˆ˜fromRaw ctx Q wsQ)
  toRawâˆ˜fromRaw ctx (x â¦… y â¦† P) (xâˆˆctx , wsP)
    = input (_ , refl , toNameâˆ˜fromName xâˆˆctx)
            (toRawâˆ˜fromRaw (y âˆ· ctx) P wsP)
  toRawâˆ˜fromRaw ctx (x âŸ¨ y âŸ© P) (xâˆˆctx , yâˆˆctx , wsP)
    = output (_ , refl , toNameâˆ˜fromName xâˆˆctx)
             (_ , refl , toNameâˆ˜fromName yâˆˆctx)
             (toRawâˆ˜fromRaw ctx P wsP)
